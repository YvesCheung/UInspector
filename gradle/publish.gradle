apply plugin: 'maven-publish'
apply plugin: 'signing'

group = "io.github.yvescheung"                               // Maven Group ID for the artifact
version = libraryVersion
final String developerId = 'YvesCheung'
final String developerName = 'YvesCheung'
final String developerEmail = '975135274@qq.com'
final String licenseName = 'The Apache License, Version 2.0'
final String licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0'

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        getArchiveBaseName().set(libraryName)
        from android.sourceSets.main.java.srcDirs
    }
    task androidNativeJar(type: Jar) {
        classifier = 'so'
        from(new File(buildDir, 'libs'))
        include("**/*.so")
    }
    ext.libraryOutputFile = "$buildDir/outputs/aar/${project.getName()}-release.aar"
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        getArchiveBaseName().set(libraryName)
        from sourceSets.main.allSource
    }
    ext.libraryOutputFile = "$buildDir/libs/${project.getName()}-${version}.jar"
}
println("artifect: $libraryOutputFile")

try {
    ext['ossrhUsername'] = ""
    ext['ossrhPassword'] = ""
    ext['signing.keyId'] = ""
    ext['signing.password'] = ""
    ext['signing.secretKeyRingFile'] = ""
    //Override these ext.variable by local.properties
    File localProperties = project.rootProject.file('local.properties')
    if (localProperties.exists()) {
        Properties properties = new Properties()
        properties.load(localProperties.newDataInputStream())
        for (Map.Entry<String, String> entry : properties.entrySet()) {
            ext[entry.key] = entry.value
        }
    }
} catch (Throwable ignore) {
}


afterEvaluate {
    publishing {
        repositories {
            maven {
                // You only need this if you want to publish snapshots, otherwise just set the URL
                // to the release repo directly
                url = version.endsWith('SNAPSHOT')
                        ? "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                        : "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"

                // The username and password we've fetched earlier
                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }

        publications {
            MyPublication(MavenPublication) {
                from components.release
                groupId group
                artifactId libraryName
                artifact sourcesJar
                version libraryVersion
                pom {
                    name = libraryName
                    description = libraryDescription
                    url = siteUrl

                    developers {
                        developer {
                            id = developerId
                            name = developerName
                            email = developerEmail
                        }
                    }

                    scm {
                        connection = gitUrl
                        developerConnection = gitUrl
                        url = siteUrl
                    }

                    licenses {
                        license {
                            name = licenseName
                            url = licenseUrl
                        }
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}